Arts2{

INCLUDE "general/general.arts"
INCLUDE "general/continua.arts"
INCLUDE "general/agendas.arts"
INCLUDE "general/planet_earth.arts"

Copy(iy_main_agenda, iy_main_agenda__Emission)
Copy(iy_surface_agenda, iy_surface_agenda__UseSurfaceRtprop)
Copy(ppath_agenda, ppath_agenda__FollowSensorLosPath)
Copy(ppath_step_agenda, ppath_step_agenda__GeometricPath)
Copy(iy_space_agenda, iy_space_agenda__CosmicBackground)

AgendaSet(abs_xsec_agenda){
Ignore(abs_nlte)
abs_xsec_per_speciesInit
abs_xsec_per_speciesAddConts
}



StringSet(iy_unit, "RJBT")
IndexSet(atmosphere_dim, 1)
IndexSet(stokes_dim, 1)
VectorNLinSpace(f_grid, 1000, 10e9, 1000e9)
sensorOff

abs_speciesSet(species=["H2O"])
ReadARTSCAT(filename="single_line.H2O.xml.gz",
            normalization_option="VVH",
            mirroring_option="None",
            lineshapetype_option="LP",
            cutoff_option="None",
    )
abs_lines_per_speciesCreateFromLines
lbl_checkedCalc
abs_xsec_agenda_checkedCalc

AgendaSet(propmat_clearsky_agenda){
Ignore(rtp_mag)
Ignore(rtp_los)
propmat_clearskyInit
propmat_clearskyAddXsecAgenda
propmat_clearskyAddLines
}

jacobianOff
cloudboxOff
MatrixCreate(profile)
ReadXML(profile, "profile.xml")
atm_fields_compactFromMatrix(gin1=profile,
                             field_names=["T","z","abs_species-H2O", "ignore"])
atm_fields_compactAddConstant(name="abs_species-O2",
                              value=0.2095)
atm_fields_compactAddConstant(name="abs_species-N2",
                              value=0.7808)                             
AtmFieldsAndParticleBulkPropFieldFromCompact
MatrixSetConstant(z_surface, 1, 1, 80.0)
MatrixSetConstant(sensor_pos, 1, 1, 100.0)
MatrixSetConstant(sensor_los, 1, 1, 0.0)
atmfields_checkedCalc(bad_partition_functions_ok=1)
atmgeom_checkedCalc
cloudbox_checkedCalc
propmat_clearsky_agenda_checkedCalc
sensor_checkedCalc

# Run simulation and return value TODO include transmission, jacobian
yCalc
WriteXML(output_file_format="ascii", in=y)

}
